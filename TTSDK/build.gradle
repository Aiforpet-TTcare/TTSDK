plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 39
        versionName "1.8.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndkVersion '21.3.6528147'
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a'

        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17'
            }
        }


    }

    namespace 'com.aiforpet.pet'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            assets.srcDirs += file("${project(':TTSDK').projectDir}/src/main/assets")
        }
    }


    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    packagingOptions {
        pickFirst 'assets/han.json'
        pickFirst 'assets/images/*'

        jniLibs {
            pickFirsts += ['**/*.so']
        }
        resources {
            excludes += [
                    'META-INF/DEPENDENCIES',
                    'META-INF/NOTICE',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/NOTICE.txt',
                    'META-INF/ASL2.0',
                    'META-INF/common_release.kotlin_module',
                    'build.xml'
            ]
        }
    }


    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            debuggable true
            buildConfigField "String", "BASE_URL_STATUS", '"https://status.tdogtdog.com/hospital-us/app-status"'
            buildConfigField "String", "BASE_URL_SDK", '"https://dev-sdk.ttcare.co.kr"'
            buildConfigField "String", "REACT_URL", '"https://dev-web.ttcare.co.kr/react"'
            buildConfigField "String", "VERSION_CODE", '"1"'
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "BASE_URL_STATUS", '"https://status.tdogtdog.com/hospital-us/app-status"'
            buildConfigField "String", "BASE_URL_SDK", '"https://dev-sdk.ttcare.co.kr"'
            buildConfigField "String", "REACT_URL", '"https://dev-web.ttcare.co.kr/react"'
            buildConfigField "String", "VERSION_CODE", '"1"'
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.Aiforpet-TTcare'
                artifactId = 'TTSDK'
                version = 'v1.6.9'

                artifact("$buildDir/outputs/aar/TTSDK-release.aar") {
                    builtBy tasks.named("assembleRelease")
                }

                // 의존성 추가
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each { dep ->
                        if (dep.group) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dep.group)
                            dependencyNode.appendNode('artifactId', dep.name)
                            dependencyNode.appendNode('version', dep.version)
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    implementation 'com.airbnb.android:lottie:5.2.0'
    def camerax_version = "1.2.0-alpha04"
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation 'com.squareup.picasso:picasso:2.8'

    implementation 'org.tensorflow:tensorflow-lite:2.8.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.8.0'
    implementation 'org.tensorflow:tensorflow-lite-support:0.3.1'
    implementation 'org.tensorflow:tensorflow-lite-task-vision:0.3.0'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.3.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu-delegate-plugin:0.3.0'



    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
    implementation files('libs/classes.jar')




}